plugins {
    id 'eclipse'
    id 'java-library'
    id 'signing'
    id 'maven-publish'
}

group = "net.hsasaki0316"
archivesBaseName = "javacle"
version = "1.0.0"

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'junit:junit:4.13.2'
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'javacle'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'Javacle'
                description = 'We created this library to use Oracle DB conditional functions in Java'
                url = 'https://github.com/hsasaki0316/javacle'
                licenses {
                    license {
                        name = 'The Apache Software License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'hsasaki0316'
                        name = 'Hiroyuki Sasaki'
                        email = 'hsasaki2424@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:https://github.com/hsasaki0316/javacle.git'
                    developerConnection = 'scm:https://github.com/hsasaki0316/javacle.git'
                    url = 'scm:https://github.com/hsasaki0316/javacle.git'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
            def snapshotsRepoUrl = 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

            // authentication(userName: ossrhUsername, password: ossrhPassword)
            credentials {
                username ossrhUsername
                password ossrhPassword
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}